use ::BCMaintenanceWidget/lib/BCMaintenanceWidget.bbj::BCMaintenanceWidget
use ::bbwindowutils.bbj::BBWindowUtils

use com.basiscomponents.bc.BusinessComponent

? 'HIDE'

bbjHome! = System.getProperty("basis.BBjHome")

barista! = bbjHome! + "/barista/ "
prefix barista! + pfx

user$ = "admin"
password$ = "admin123"

bar! = new ::sys/prog/bao_barista.bbj::Barista(1); rem with no IO
bar!.setUserCredentials(user$, password$)

rem don't need to keep the Barista object around
bar!.destroy()
bar! = null()


sysgui! = BBjAPI().openSysGui("X0")
wnd! = sysgui!.addWindow(0,0,1400,800,"BCMaintenanceWidget Demo")

lb! = wnd!.addListButton(wnd!.getAvailableControlID(),65,0,200,400,"")
lb!.setCallback(lb!.ON_LIST_SELECT,"onBCSelect")

text! = wnd!.addStaticText(wnd!.getAvailableControlID(),5,5,60,25,"BC wählen")

wnd!.setCallback(BBjAPI.ON_CLOSE,"byebye")
wnd!.setCallback(BBjAPI.ON_RESIZE,"resize")

wnd2! = wnd!.addChildWindow(wnd!.getAvailableControlID(),0,25,1400,775,"",$00000800$,BBjAPI().getSysGui().getAvailableContext())
path! = bbjHome! + "/apps/examples/classes/user/"

gosub readfromPath

declare BCMaintenanceWidget editor!
BBWindowUtils.centerWindow(wnd!)
editor! = new BCMaintenanceWidget(wnd2!)

lb!.selectIndex(0)
bc! = lb!.getItemAt(0)
gosub setBC

process_events

onBCSelect:
    ev! = BBjAPI().getLastEvent()
    bc! = ev!.getSelectedItem()
    gosub setBC
return

setBC:
    chdir(bbjHome! + "/barista/")
    toEval! = "new ::" + bc! + ".bbj::" + bc! + "()"
    bc! = eval(toEval!)
    editor!.setBC(bc!)
return

readfromPath:
declare BBjClientFileSystem filesys!
declare BBjClientFile file!
declare BBjVector files!

    filesys! = BBjAPI().getThinClient().getClientFileSystem()
    file! = filesys!.getClientFile(path!)
    files! = file!.listFiles()

    if files!.isEmpty() then 
        a = msgbox("Please make sure your Barista is synced and you have the Examples downloaded",1,"Error")
        return
    endif

    for i = 0 to files!.size() -1 
        value! = str(files!.get(i))
        value! = value!.replaceAll(path!,"")
        value! = value!.replaceAll(".bbj","")
        lb!.addItem(value!)
    next i
return

byebye:
    release

resize:
    ev! = BBjAPI().getLastEvent()
    h = ev!.getHeight()
    w = ev!.getWidth()
    wnd2!.setSize(w,h-50)
    editor!.getgridex().setSize(w,h-50)
return
